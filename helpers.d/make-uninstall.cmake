include_guard(GLOBAL)

#
# add a make uninstall target that removes files based on the install_manifest generated by CMake
#
function(cmake_add_uninstall_target)
  if(WIN32)
    file(WRITE ${CMAKE_BINARY_DIR}/uninstall.bat
"@echo off\n\
setlocal EnableExtensions EnableDelayedExpansion\n\
for /F \"tokens=*\" %%A in (install_manifest.txt) do (\n\
  set \"l=%%A\"\n\
  set \"m=!l:/=\\!\"\n\
  del /S /Q \"!m!\"\n\
)")

    add_custom_target(uninstall
      COMMAND ${CMAKE_BINARY_DIR}/uninstall.bat
      WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
    )

  else(WIN32)
    file(WRITE ${CMAKE_BINARY_DIR}/uninstall.sh
"for f in $(cat install_manifest.txt | sort -n | uniq); do\n\
  echo \"-- Removing: $f\";\n\
  rm \"$f\"\n\
done")

    add_custom_target(uninstall
      COMMAND sh ${CMAKE_BINARY_DIR}/uninstall.sh
      WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
    )
  endif(WIN32)

endfunction(cmake_add_uninstall_target)